apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

// Optionally load the information required for signing the apk.  This is optional because
// travis runs the debug build and doesn't need this to be checked in.
def keystoreProperties = new Properties()
def keystorePropertiesFile = new File("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
else {
    logger.warn("No keystore.properties file exists, only debug builds are available")
}

android {
    signingConfigs {

        config_biocollect {
            keyAlias keystoreProperties['biocollect.keyAlias']
            keyPassword keystoreProperties['biocollect.keyPassword']
            if (keystoreProperties['biocollect.storeFile']) {
                storeFile file(keystoreProperties['biocollect.storeFile'])
            }
            storePassword keystoreProperties['biocollect.storePassword']
        }

        config_biocontrolhub {
            keyAlias keystoreProperties['biocontrolhub.keyAlias']
            keyPassword keystoreProperties['biocontrolhub.keyPassword']
            if (keystoreProperties['biocontrolhub.storeFile']) {
                storeFile file(keystoreProperties['biocontrolhub.storeFile'])
            }
            storePassword keystoreProperties['biocontrolhub.storePassword']
        }

        config_bilbyblitz {
            keyAlias keystoreProperties['bilbyblitz.keyAlias']
            keyPassword keystoreProperties['bilbyblitz.keyPassword']
            if (keystoreProperties['bilbyblitz.storeFile']) {
                storeFile file(keystoreProperties['bilbyblitz.storeFile'])
            }

            storePassword keystoreProperties['bilbyblitz.storePassword']
        }
    }
    compileSdkVersion 27
    flavorDimensions "versionCode"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 27
        applicationId 'au.org'
        multiDexEnabled true
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        biocollect {
            applicationIdSuffix '.ala.biocollect'
            versionCode 10
            versionName '1.1.6'
            signingConfig signingConfigs.config_biocollect
        }
        ozatlas {
            applicationIdSuffix '.ala.mobile.ozatlas'
            versionCode 14
            versionName '2.1.2'

        }
        biocontrolhub {
            applicationIdSuffix '.ala.biocontrolhub'
            versionCode 5
            versionName '1.0.3'
            signingConfig signingConfigs.config_biocontrolhub

        }
        bilbyblitz {
            applicationIdSuffix '.ala.bilbyblitz'
            versionCode 22
            versionName '1.1'
            signingConfig signingConfigs.config_bilbyblitz

        }
    }

    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //Realm
        classpath "io.realm:realm-gradle-plugin:4.2.0"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.parceler:parceler-api:1.1.9'
    annotationProcessor 'org.parceler:parceler:1.1.9'
    //butter knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //dagger
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    //rxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    //image download
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    //ciruclar imageview
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    //cardview
    implementation 'com.android.support:cardview-v7:27.1.1'
    //top snackbar
    implementation 'com.androidadvance:topsnackbar:1.1.1'
    //Event Bus
    implementation 'org.greenrobot:eventbus:3.1.1'
    //android support
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.android.gms:play-services-places:11.8.0'
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
    // Unit testing dependencies.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.6'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'


}
