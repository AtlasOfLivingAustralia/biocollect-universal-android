apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

// Optionally load the information required for signing the apk.  This is optional because
// travis runs the debug build and doesn't need this to be checked in.
def keystoreProperties = new Properties()
def keystorePropertiesFile = new File("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
else {
    logger.warn("No keystore.properties file exists, only debug builds are available")
}

android {
    signingConfigs {

        config_biocollect {
            keyAlias keystoreProperties['biocollect.keyAlias']
            keyPassword keystoreProperties['biocollect.keyPassword']
            if (keystoreProperties['biocollect.storeFile']) {
                storeFile file(keystoreProperties['biocollect.storeFile'])
            }
            storePassword keystoreProperties['biocollect.storePassword']
        }

        config_biocontrolhub {
            keyAlias keystoreProperties['biocontrolhub.keyAlias']
            keyPassword keystoreProperties['biocontrolhub.keyPassword']
            if (keystoreProperties['biocontrolhub.storeFile']) {
                storeFile file(keystoreProperties['biocontrolhub.storeFile'])
            }
            storePassword keystoreProperties['biocontrolhub.storePassword']
        }

        config_bilbyblitz {
            keyAlias keystoreProperties['bilbyblitz.keyAlias']
            keyPassword keystoreProperties['bilbyblitz.keyPassword']
            if (keystoreProperties['bilbyblitz.storeFile']) {
                storeFile file(keystoreProperties['bilbyblitz.storeFile'])
            }

            storePassword keystoreProperties['bilbyblitz.storePassword']
        }

        config_ozatlas {
            keyAlias keystoreProperties['ozatlas.keyAlias']
            keyPassword keystoreProperties['ozatlas.keyPassword']
            if (keystoreProperties['ozatlas.storeFile']) {
                storeFile file(keystoreProperties['ozatlas.storeFile'])
            }

            storePassword keystoreProperties['ozatlas.storePassword']
        }
    }
    compileSdkVersion 33
    flavorDimensions "versionCode"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        applicationId 'au.org'
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'au.org.ala'
        ]
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        biocollect {
            applicationIdSuffix '.ala.biocollect'
            versionCode 13
            versionName '1.2.1'
            signingConfig signingConfigs.config_biocollect
        }
        biocontrolhub {
            applicationIdSuffix '.ala.biocontrolhub'
            versionCode 8
            versionName '1.2.1'
            signingConfig signingConfigs.config_biocontrolhub

        }
        bilbyblitz {
            applicationIdSuffix '.ala.bilbyblitz'
            versionCode 26
            versionName '1.2.1'
            signingConfig signingConfigs.config_bilbyblitz

        }
    }
    packagingOptions {
        resources {
            pickFirsts += ['META-INF/rxjava.properties']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //Realm
        classpath "io.realm:realm-gradle-plugin:4.2.0"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.parceler:parceler-api:1.1.13'
    annotationProcessor 'org.parceler:parceler:1.1.13'
    //butter knife
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    //dagger
    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    //rxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // OIDC authentication & JWT
    implementation 'net.openid:appauth:0.11.1'
    implementation 'com.auth0.android:jwtdecode:2.0.1'
    //image download
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    //ciruclar imageview
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    //cardview
    implementation 'androidx.cardview:cardview:1.0.0'
    //top snackbar
    implementation 'com.androidadvance:topsnackbar:1.1.1'
    // loading button
    implementation 'br.com.simplepass:loading-button-android:2.2.0'
    //Event Bus
    implementation 'org.greenrobot:eventbus:3.1.1'
    //android support
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.work:work-runtime:2.7.1"
    implementation 'com.google.android.gms:play-services-places:17.0.0'
    implementation 'com.google.android.gms:play-services-location:21.0.0'
    implementation 'com.google.firebase:firebase-core:17.5.1'
    implementation 'com.google.firebase:firebase-messaging:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.1'
    // Unit testing dependencies.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'

    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.0'
    // TODO refactor to make this androidTestImplementation

}